apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'


/*
 * Gets the version name from the latest Git tag
 */
def getVersionName = {
    def stdout = new ByteArrayOutputStream()
    try {
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (e) {
        println("Can't get version from git: " + e);
        return "adhoc"
    }
}

allprojects{
    version = "${getVersionName()}"
}

android {
    compileSdkVersion 23
    buildToolsVersion '25.0.0'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 0
        versionName version
        buildConfigField("int", "API_VERSION", "2")
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            // proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}



configurations {
    javadocDeps
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    title = "Android NearIt SDK $version API"
    description = "Generates JavaDoc for $version"
    println "Generating Javadocs for $version"
    // failOnError false
    options.author = true
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
    classpath += configurations.javadocDeps
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

// Upload to bintray

group = 'it.near.sdk.core'

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    pkg {
        repo = 'maven'
        name = 'it.near.sdk.core'

        licenses = ['MIT']
        vcsUrl = 'https://github.com/catt-stefano/near-sdk-core.git'
        websiteUrl = 'https://github.com/catt-stefano/near-sdk-core'
    }
    configurations = ['archives']
}

// Upload docs on ftp

repositories{
    mavenCentral()
}
configurations {
    ftpAntTask
}
dependencies{
    ftpAntTask ("org.apache.ant:ant-commons-net:1.8.4") {
        module("commons-net:commons-net:1.4.1") {
            dependencies "oro:oro:2.0.8:jar"
        }
    }
}

task uploadDocs {
    mustRunAfter generateJavadocs
    description = "Upload javadocs to our ftp server"
    def ftpHost = properties.getProperty("docs.ftp.host")
    def ftpDir = properties.getProperty("docs.ftp.dir")
    def ftpUser = properties.getProperty("docs.ftp.user")
    def ftpPassword = properties.getProperty("docs.ftp.password")
    doLast{
        ant {
            taskdef(name: 'ftp',
                    classname: 'org.apache.tools.ant.taskdefs.optional.net.FTP',
                    classpath: configurations.ftpAntTask.asPath)
            ftp(server: ftpHost, userid: ftpUser, password: ftpPassword, remoteDir: ftpDir, passive: 'yes') {
                fileset(dir: 'build/docs/javadoc/')
            }
        }
    }
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:support-annotations:24.2.1'
    javadocDeps 'com.android.support:support-annotations:23.3.0'
    javadocDeps 'org.altbeacon:android-beacon-library:2.9.1'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.loopj.android:android-async-http:1.4.9'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'org.altbeacon:android-beacon-library:2.9.1'
    compile 'com.google.firebase:firebase-messaging:9.6.1'
    compile 'com.google.firebase:firebase-core:9.6.1'
    compile 'com.google.android.gms:play-services-location:9.6.1'

    // remove this
    compile 'com.jraska:console:0.4.3'

}


buildscript {
    repositories {
        jcenter()
        flatDir{
            dirs 'libs'
        }
    }
}